buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
    }
}

description = 'JMD is a general purpose Java bytecode deobfuscation tool'

subprojects {
    apply plugin: 'java'
    apply plugin: 'findbugs'
    apply plugin: 'pmd'
    apply plugin: 'jacoco'
    apply plugin: 'com.github.kt3k.coveralls'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    version = '1.6'

    repositories {
        mavenCentral()
    }

    test.onlyIf {
        !Boolean.getBoolean('skip.tests')
    }

    findbugs {
        ignoreFailures = true
        effort = 'max'
    }

    pmd {
        ignoreFailures = true
        ruleSets = [
                'java-basic',
                'java-braces',
                'java-clone',
                'java-codesize',
                'java-comments',
                'java-controversial',
                'java-design',
                'java-empty',
                'java-finalizers',
                'java-imports',
                'java-j2ee',
                'java-javabeans',
                'java-junit',
                'java-logging-jakarta-commons',
                'java-logging-java',
                'java-migrating',
                'java-naming',
                'java-optimizations',
                'java-strictexception',
                'java-strings',
                'java-sunsecure',
                'java-typeresolution',
                'java-unnecessary',
                'java-unusedcode'
        ]
    }

    tasks.withType(Pmd) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    tasks.coveralls {
        dependsOn 'check'
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = false
        }
    }
}

project(':cmd') {
    dependencies {
        compile project(':core')
    }

    jar {
        manifest {
            attributes 'Implementation-Title': 'net.contra.jmd',
                    'Implementation-Version': version,
                    'Main-Class': 'net.contra.jmd.Deobfuscator'
        }

        from {
            configurations.compile.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
    }
}

project(':core') {
    dependencies {
        compile 'com.google.dagger:dagger:2.0.2'
        compile 'org.apache.bcel:bcel:5.2'
        compile 'commons-io:commons-io:2.4'

        testCompile 'org.testng:testng:6.9.10'
    }
}

project(':gui') {
}